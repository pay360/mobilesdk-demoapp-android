apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "22.0.1"

    defaultConfig {

        applicationId "com.pay360.sdk.demo"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        release {
            storeFile file('./../keys.jks')
            storePassword 'paypoint'
            keyAlias 'paypoint'
            keyPassword 'paypoint'
        }
    }

    buildTypes {
        def serverUrl = project.hasProperty('pay360.server.url') ? project.property('pay360.server.url') : ""
        def merchantUrl = project.hasProperty('pay360.merchant.url') ? project.property('pay360.merchant.url') : ""
        def installationId = project.hasProperty('pay360.installationId') ? project.property('pay360.installationId') : ""

        release {
            resValue "string", "build_url_server", serverUrl
            resValue "string", "build_url_merchant", merchantUrl
            resValue "string", "build_installation_id", installationId

            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            resValue "string", "build_url_server", serverUrl
            resValue "string", "build_url_merchant", merchantUrl
            resValue "string", "build_installation_id", installationId
        }
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    if (project.hasProperty('pay360.sdk.project.name')) {
        flatDir {
            dirs '../../' + project.property('pay360.sdk.project.name') + '/library/build/outputs/aar'
        }
    }
}

dependencies {

    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.facebook.rebound:rebound:0.3.7'

    // check devEnv property - used to change behaviour for local\ci\app build
    if (project.hasProperty('devEnv')) {

        if (project.property('devEnv').equals('local')) {
            // pull in aar from local file system during development
            compile fileTree(dir: 'libs', include: ['*.jar'])
            compile(name: 'paypoint_sdk', ext: 'aar')

            // also need to manually pull in dependencies
            compile 'com.squareup.retrofit:retrofit:1.9.0'
            compile 'com.google.code.gson:gson:2.2.4'
            compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
            compile 'com.squareup.okhttp:okhttp:2.4.0'
            compile 'com.netflix.rxjava:rxjava-android:0.20.7'
        } else if (project.property('devEnv').equals('ci')) {
            // for ci builds use the most recent version of the SDK
            compile 'net.paypoint:mobilesdk-android:+'
        }
    } else {
        // point to a specified version of the SDK - this is what apps using the SDK should do
        compile 'net.paypoint:mobilesdk-android:1.0.0'
    }
}
